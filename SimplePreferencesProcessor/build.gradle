apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile 'com.github.wrdlbrnft:code-builder:0.1.0.2'
}

final bintrayUser = hasProperty('bintray_user') ? property('bintray_user') : ''
final bintrayApiKey = hasProperty('bintray_api_key') ? property('bintray_api_key') : ''
final versionName = hasProperty('version_name') ? property('version_name') : ''

task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier 'source'
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'simple-preferences-processor'
            version versionName
            artifact(sourceJar)
            artifact("$buildDir/libs/SimplePreferencesProcessor.jar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                project.configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode 'groupId', it.group
                        dependencyNode.appendNode 'artifactId', it.name
                        dependencyNode.appendNode 'version', it.version
                        final dependencyArtifact = it.artifacts[0];
                        if(dependencyArtifact != null) {
                            if (dependencyArtifact.classifier != null) {
                                dependencyNode.appendNode 'classifier', dependencyArtifact.classifier
                            }
                            if (dependencyArtifact.extension != null) {
                                dependencyNode.appendNode 'type', dependencyArtifact.extension
                            }
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'SimplePreferencesProcessor'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Wrdlbrnft/SimplePreferences'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}